plugins {
    id "com.android.library"
    id "kotlin-android"
    id "jacoco"
    id "org.jetbrains.dokka"
    id "maven-publish"
}

apply from: "$rootDir/gradle/library-artifacts.gradle"
//apply from: "$rootDir/gradle/jacoco-android.gradle"

android {
    namespace 'com.gyurigrell.rxreactor3'
    compileSdkVersion buildConfig.compileSdk

    defaultConfig {
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lint {
        abortOnError false
        baseline file('lint-baseline.xml')
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    api project(path: ':rxreactor3')

    api libs.rx3.java
    implementation libs.rx3.android

//    testImplementation libs.junit
}

tasks.withType(Test).configureEach {
    jacoco.includeNoLocationClasses = true
}

dokkaJavadoc.configure {
    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
            includeNonPublic.set(false)
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            RxReactor3Android(MavenPublication) {
                groupId = project.group
                artifactId = archivesBaseName

                from components.release

                pom {
                    name = "RxReactor (RxJava 3) for Android"
                    description = "A Kotlin framework for a reactive and unidirectional RxJava 3 application architecture"
                    url = POM_URL

                    licenses {
                        license {
                            name = POM_LICENSE_NAME
                            url = POM_LICENSE_URL
                        }
                    }

                    developers {
                        developer {
                            id = POM_DEVELOPER_ID
                            name = POM_DEVELOPER_NAME
                            email = POM_DEVELOPER_EMAIL
                        }
                    }

                    scm {
                        connection = POM_SCM_CONNECTION
                        developerConnection = POM_SCM_CONNECTION
                        url = POM_SCM_URL
                    }
                }
            }
        }
    }

    signing {
        def signingKeyId = findProperty("signingKeyId")
        def signingKey = findProperty("signingKey")
        def signingPassword = findProperty("signingPassword")
        useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)

        sign publishing.publications
    }

    tasks.withType(Sign).configureEach {
        onlyIf { findProperty("signingKey") != null }
    }
}

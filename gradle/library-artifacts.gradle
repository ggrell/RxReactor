if (project.plugins.hasPlugin('com.android.library')) {
    android.libraryVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${archivesBaseName}-${version}.aar"
        }
    }

    def siteUrl = "https://github.com/ggrell/RxReactor"
    def gitUrl = "https://github.com/ggrell/RxReactor.git"

    install {
        repositories {
            mavenInstaller {
                pom {
                    project {
                        packaging 'aar'

                        groupId project.group
                        artifactId archivesBaseName

                        url siteUrl

                        licenses {
                            license {
                                name 'BSD 3-Clause License'
                                url 'https://github.com/ggrell/RxReactor/blob/master/LICENSE'
                            }
                        }

                        developers {
                            developer {
                                id 'ggrell'
                                name 'Gyuri Grell'
                                email 'ggrell@pobox.com'
                            }
                        }

                        scm {
                            connection gitUrl
                            developerConnection gitUrl
                            url siteUrl
                        }
                    }
                }
            }
        }
    }

    afterEvaluate {
        dokka {
            outputFormat = 'javadoc'
            includeNonPublic = false
            outputDirectory = "$buildDir/javadoc"
        }

        task sourcesJar(type: Jar) {
            classifier = 'sources'
            from android.sourceSets.main.java.srcDirs
        }

        task javadocJar(type: Jar, dependsOn: dokka) {
            classifier = 'javadoc'
            from dokka.outputDirectory
        }

        artifacts {
            archives javadocJar
            archives sourcesJar
        }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'jacoco'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'maven-publish'
apply from: "$rootDir/gradle/library-artifacts.gradle"
apply from: "$rootDir/gradle/jacoco-android.gradle"

android {
    compileSdkVersion buildConfig.compileSdk

    defaultConfig {
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        debug {
            testCoverageEnabled = true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
        baseline file("lint-baseline.xml")
    }
}

dependencies {
    implementation project(path: ':rxreactor2')
    implementation deps.kotlin.stdlib.jdk
    implementation deps.rx2.android

    testImplementation deps.junit

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

jacoco {
    toolVersion = versions.jacoco
}

afterEvaluate {
    publishing {
        publications {
            rxreactor2Android(MavenPublication) {
                groupId = project.group
                artifactId = archivesBaseName

                from components.release
                artifact sourcesJar
                artifact javadocJar

                pom {
                    name = "RxReactor (RxJava 2) for Android"
                    description = "A Kotlin framework for a reactive and unidirectional RxJava 2 application architecture"
                    url = siteUrl

                    licenses {
                        license {
                            name = 'BSD 3-Clause License'
                            url = 'https://github.com/ggrell/RxReactor/blob/master/LICENSE'
                        }
                    }

                    developers {
                        developer {
                            id = 'ggrell'
                            name = 'Gyuri Grell'
                            email = 'ggrell@pobox.com'
                        }
                    }

                    scm {
                        connection = gitUrl
                        developerConnection = gitUrl
                        url = siteUrl
                    }
                }
            }
        }
    }
}

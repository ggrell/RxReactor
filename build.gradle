// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.buildConfig = [
        minSdk    : 15,
        compileSdk: 31,
        targetSdk : 31
    ]

    ext.versions = [
        kotlin: "1.6.10",
        jacoco: "0.8.5",
        detekt: "1.19.0",
    ]

    ext.deps = [
        junit   : 'junit:junit:4.13.2',

        kotlin  : [
            stdlib: [
                common: "org.jetbrains.kotlin:kotlin-stdlib-common:${versions.kotlin}",
                jdk   : "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}",
            ],
            test  : [
                common     : "org.jetbrains.kotlin:kotlin-test-common:${versions.kotlin}",
                annotations: "org.jetbrains.kotlin:kotlin-test-annotations-common:${versions.kotlin}",
                jvm        : "org.jetbrains.kotlin:kotlin-test:${versions.kotlin}",
                junit      : "org.jetbrains.kotlin:kotlin-test-junit:${versions.kotlin}",
                reflect    : "org.jetbrains.kotlin:kotlin-reflect:${versions.kotlin}",
            ],
        ],

        androidx: [
            activity   : "androidx.activity:activity-ktx:1.4.0",
            annotations: "androidx.annotation:annotation:1.3.0",
            appcompat  : "androidx.appcompat:appcompat:1.3.1",
            core       : "androidx.core:core-ktx:1.7.0",
            design     : "com.google.android.material:material:1.4.0",
            fragment   : "androidx.fragment:fragment-ktx:1.3.6",
            viewModel  : 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0',
        ],

        rx1     : [
            java   : 'io.reactivex:rxjava:1.3.8',
            kotlin : 'io.reactivex:rxkotlin:1.0.0',
            android: 'io.reactivex:rxandroid:1.2.1',
            relay  : 'com.jakewharton.rxrelay:rxrelay:1.2.0'
        ],

        rx2     : [
            java            : 'io.reactivex.rxjava2:rxjava:2.2.21',
            kotlin          : 'io.reactivex.rxjava2:rxkotlin:2.4.0',
            android         : 'io.reactivex.rxjava2:rxandroid:2.1.1',
            relay           : 'com.jakewharton.rxrelay2:rxrelay:2.1.1',
            binding         : 'com.jakewharton.rxbinding3:rxbinding-core:3.1.0',
            bindingAppcompat: 'com.jakewharton.rxbinding3:rxbinding-appcompat:3.1.0',
            bindingDesign   : 'com.jakewharton.rxbinding3:rxbinding-design-kotlin:3.1.0'
        ],

        rx3     : [
            java            : 'io.reactivex.rxjava3:rxjava:3.0.13',
            kotlin          : 'io.reactivex.rxjava3:rxkotlin:3.0.1',
            android         : 'io.reactivex.rxjava3:rxandroid:3.0.0',
            relay           : 'com.jakewharton.rxrelay3:rxrelay:3.0.1',
            binding         : 'com.jakewharton.rxbinding4:rxbinding-core:4.0.0',
            bindingAppcompat: 'com.jakewharton.rxbinding4:rxbinding-appcompat:4.0.0',
            bindingDesign   : 'com.jakewharton.rxbinding4:rxbinding-design-kotlin:4.0.0'
        ]
    ]

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.4'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.19.0"
    id "org.jetbrains.dokka" version "1.6.0"
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    apply plugin: "maven-publish"
    apply plugin: "signing"
    apply plugin: "io.gitlab.arturbosch.detekt"

    group = "com.gyurigrell.rxreactor"
    def isRelease = Boolean.valueOf(project.hasProperty("release") ? project.property("release") as String : "false")
    version = project.property("VERSION") + (isRelease ? "" : "-SNAPSHOT")

    detekt {
        toolVersion = versions.detekt
        config = files("${rootProject.projectDir}/detekt.yml")
        baseline = file("${rootProject.projectDir}/detekt-baseline.xml")
        buildUponDefaultConfig = true
    }

    dependencies {
        detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:$versions.detekt"
    }

    tasks.named("detekt").configure {
        reports {
            xml.required.set(true)
            xml.outputLocation.set(file("build/reports/detekt/detekt.xml"))

            html.required.set(true)
            html.outputLocation.set(file("build/reports/detekt/detekt.html"))

            sarif.required.set(false)
            txt.required.set(false)
        }
    }

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/ggrell/RxReactor")
                credentials {
                    username = project.findProperty("gpr.username")
                    password = project.findProperty("gpr.key")
                }
            }
            maven {
                name = "MavenCentralSnapshots"
                url = uri("https://oss.sonatype.org/content/repositories/snapshots")
                credentials {
                    username = project.findProperty("ossrh.username")
                    password = project.findProperty("ossrh.password")
                }
            }
            maven {
                name = "MavenCentralReleases"
                url = uri("https://oss.sonatype.org/service/local/staging/deploy/maven2")
                credentials {
                    username = project.findProperty("ossrh.username")
                    password = project.findProperty("ossrh.password")
                }
            }
        }
    }
}


apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'jacoco'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'maven-publish'

dependencies {
    implementation deps.kotlin.stdlib.jdk
    implementation deps.rx2.java
    implementation deps.rx2.relay

    testImplementation deps.junit
}

jacoco {
    toolVersion = versions.jacoco
}

jacocoTestReport {
    reports {
        xml.enabled = true
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier "javadoc"
    from dokkaJavadoc.outputDirectory
}

publishing {
    publications {
        rxreactor1(MavenPublication) {
            groupId = project.group
            artifactId = archivesBaseName

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = "RxReactor (RxJava 2)"
                description = "A Kotlin framework for a reactive and unidirectional RxJava 2 application architecture"
                url = siteUrl

                licenses {
                    license {
                        name = 'BSD 3-Clause License'
                        url = 'https://github.com/ggrell/RxReactor/blob/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id = 'ggrell'
                        name = 'Gyuri Grell'
                        email = 'ggrell@pobox.com'
                    }
                }

                scm {
                    connection = gitUrl
                    developerConnection = gitUrl
                    url = siteUrl
                }
            }
        }
    }
}